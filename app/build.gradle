plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}
def versionProps = new Properties()
android {
    signingConfigs {
        debug {
            storeFile file('../key/PcamLeak.jks')
            storePassword 'photoncamera'
            keyPassword 'photoncamera'
            keyAlias 'key0'
        }
    }
    namespace 'com.particlesdevs.PhotonCamera'
    compileSdk 32
    def versionPropsFile = file('version.properties')
    def versionBuild
    if (versionPropsFile.canRead()) {
        Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        versionBuild = versionProps['VERSION_BUILD'].toInteger()
    } else {
        throw new FileNotFoundException("Could not read version.properties!")
    }
    ext.autoIncrementBuildNumber = {
        if (versionPropsFile.canRead()) {
            Properties versionProps = new Properties()
            versionProps.load(new FileInputStream(versionPropsFile))
            versionBuild = versionProps['VERSION_BUILD'].toInteger() + 1
            versionProps['VERSION_BUILD'] = versionBuild.toString()
            versionProps.store(versionPropsFile.newWriter(), null)
        } else {
            throw new FileNotFoundException("Could not read version.properties!")
        }
    }

    defaultConfig {
        applicationId "com.particlesdevs.PhotonCamera"
        minSdk 24
        targetSdk 32
        versionCode versionBuild
        ndk {
            abiFilters "arm64-v8a"
            abiFilters "armeabi-v7a"
        }
        versionName '0.852'

        versionProps['VERSION_NAME'] = versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                // BUILD_SHARED_LIBS=OFF to build static libaom, libavif, libjxl
                arguments "-DBUILD_SHARED_LIBS=OFF", "-DBUILD_TESTING=OFF"
                targets "NPhotonCamera"
            }
        }
    }
    gradle.taskGraph.whenReady { taskGraph ->
        if (taskGraph.hasTask(assembleDebug)) {
            autoIncrementBuildNumber()
        } else if (taskGraph.hasTask(assembleRelease)) {
            autoIncrementBuildNumber()
        }
    }
    buildTypes {
        release {
            minifyEnabled = false
            shrinkResources = false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug{
            minifyEnabled = false
            shrinkResources = false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }


    }

    externalNativeBuild {
        cmake {
            version "3.22.1"
            path "src/main/cpp/CMakeLists.txt"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    applicationVariants.all {
        variant -> variant.outputs.all {
            outputFileName = "NPhotonCamera-${versionName}${versionBuild}-${variant.name}.apk"
        }
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.4.0-alpha03"
}